   10 REM HELI 1.90 BY PHIL HITE 2021
   20 FOR g=1 TO 11: READ g$: FOR y=0 TO 7: READ x: POKE USR g$+y,x: NEXT y: NEXT g
   30 DATA "a",255,128,65,34,20,8,255,0
   40 DATA "b",170,85,170,85,170,85,170,85
   50 DATA "c",24,36,24,36,24,36,24,36
   60 DATA "d",2,0,28,186,254,190,28,0
   70 DATA "e",128,0,112,186,254,250,112,0
   80 DATA "f",60,126,255,231,231,231,165,231
   90 DATA "g",7,0,0,128,127,1,0,0
  100 DATA "h",255,32,112,236,226,252,80,252
  110 DATA "i",255,4,14,55,71,63,10,63
  120 DATA "j",224,0,0,1,254,128,0,0
  130 DATA "k",170,170,170,170,254,214,124,56
  140 DIM g(32): LET h=0: LET q=1: DIM h(10): DIM l(10): DIM n(10,9): FOR y=1 TO 10: LET l(y)=1: FOR x=1 TO 3: LET n(y,x)=CODE "A": NEXT x: NEXT y
  150 REM START
  160 LET t$=" TODAY'S GREATEST": LET g=1
  170 BORDER 0: PAPER 7: INK 1: BRIGHT 0: OVER 0: CLS 
  180 PRINT TAB 6;"        HELI    1.90"
  190 PRINT TAB 6;"   PHIL HITE 2021": PRINT 
  200 PRINT TAB 6;"O LEFT      P RIGHT"
  210 PRINT TAB 6;"SPACE PICK UP/DROP"
  220 PRINT TAB 6;"M SOUND O"
  230 IF q=1 THEN PRINT AT 5,15;"FF";
  240 IF q=0 THEN PRINT AT 5,15;"N ";
  250 PRINT " H HOLD"
  260 PRINT TAB 6;"  BUILD THE BRIDGE"
  270 PRINT TAB 6;"   STOP THE FLOOD"
  280 PRINT TAB 6;"BEWARE OF OCTOPUSES!"
  290 PRINT TAB 6;"S START     Q QUIT": PRINT 
  300 PRINT TAB 6;" ";t$
  310 FOR y=1 TO 10: PRINT AT y+11,9-LEN (STR$ (y));y;AT y+11,10;h(y);AT y+11,17;"L";l(y);AT y+11,21;: FOR x=1 TO 3: LET i=n(y,x): PRINT CHR$  i;: NEXT x: NEXT y
  320 REM KEYS
  330 LET k$=INKEY$
  340 IF k$="m" OR k$="i" OR k$="s" OR k$="q" THEN LET k$=CHR$ (CODE k$-32)
  350 IF k$="M" THEN FOR x=1 TO 1900: NEXT x: LET q=NOT q
  360 IF q=0 AND g=1 THEN PRINT AT 5,15;"N "
  370 IF q=1 AND g=1 THEN PRINT AT 5,15;"FF"
  380 IF k$="I" AND g=0 THEN GO TO 160: REM GO TO START
  390 IF k$="S" THEN CLS : RANDOMIZE : LET s=0: LET l=1: LET w=0: LET k=0: LET u=0: LET e=0: LET t=0: GO SUB 440: GO TO 540: REM GO SUB GET READY GO TO COPTER
  400 IF k$="Q" THEN STOP 
  410 IF g=1 THEN LET t$=t$(2 TO 17)+t$(1): FOR x=1 TO 1280: NEXT x: PRINT AT 11,7;t$
  420 GO TO 330
  430 REM GET READY
  440 PRINT AT 8,9;"GET READY FOR";AT 9,11;"LEVEL ";l;"!"
  450 IF q=0 THEN FOR x=1 TO 15000: NEXT x: GO TO 500
  460 BEEP .125,0: BEEP .125,4: BEEP .125,7
  470 BEEP .125,2: BEEP .125,5: BEEP .125,9
  480 BEEP .125,7: BEEP .125,9: BEEP .125,11
  490 BEEP .375,12
  500 CLS 
  510 LET c=15: LET d=0
  520 RETURN 
  530 REM COPTER
  540 LET i=INT (RND*8): LET p=INT (RND*8): IF i=7 AND p=7 THEN GO TO 540
  550 FOR y=12 TO 21: PRINT AT y,0; INK i; PAPER p;"\b\b\b\b\b\b\b\b";AT y,24;"\b\b\b\b\b\b\b\b": NEXT y
  560 PRINT AT 0,0; PAPER 0;"                                ": PRINT AT 0,0; INK 7; PAPER 0;s;AT 0,14;"L";l;AT 0,24;"HI";h
  570 PRINT AT 11,0; INK 3;"\a\a\a\a\a\a\a\a";AT 11,24;"\a\a\a\a\a\a\a\a"
  580 FOR x=1 TO 32
  590 LET g(x)=0: IF x<9 OR x>24 THEN LET g(x)=1
  600 NEXT x
  610 LET v=w
  620 IF d=0 THEN PRINT AT 2,c; INK 4;"\i\j"
  630 IF d=1 THEN PRINT AT 2,c; INK 4;"\g\h"
  640 PRINT AT 3,c+d; INK 2;"\f"
  650 IF k=1 THEN PRINT AT 4,c+d; INK 3;"\a"
  660 REM DELAY FOR COMPILER
  670 FOR x=1 TO 450: NEXT x
  680 GO SUB 830: GO SUB 980: IF t=6 THEN GO SUB 1010: LET t=0: REM GO SUB BRIDGE GO SUB PAUSE GO SUB WATER
  690 LET k$=INKEY$
  700 IF k$="m" OR k$="o" OR k$="p" OR k$="d" OR k$="h" OR k$="q" THEN LET k$=CHR$ (CODE k$-32)
  710 IF k$=" " THEN GO SUB 1240: REM GO SUB MAGNET
  720 IF k$="O" AND d=0 AND c>0 THEN PRINT AT 2,c+1;" ";AT 3,c;" ";AT 4,c;" ": LET c=c-1
  730 IF k$="O" AND d=1 THEN LET d=0: PRINT AT 3,c+1;" ";AT 4,c+1;" "
  740 IF k$="P" AND d=1 AND c<30 THEN PRINT AT 2,c;" ";AT 3,c+1;" ";AT 4,c+1;" ": LET c=c+1
  750 IF k$="P" AND d=0 THEN LET d=1: PRINT AT 3,c;" ";AT 4,c;" "
  760 IF k$="H" AND u=0 THEN PRINT AT 6,12;"HOLD IT!": PRINT AT 8,9;"ENTER CONTINUE": LET u=1
  770 IF INKEY$=CHR$ 13 AND u=1 THEN PRINT AT 6,12;"        ";AT 8,9;"              ": LET u=0
  780 IF u=1 THEN GO TO 770
  790 IF k$="M" THEN LET q=NOT q: FOR x=1 TO 1900: NEXT x
  800 IF k$="Q" THEN GO TO 160: REM GO TO START
  810 GO TO 620
  820 REM BRIDGE
  830 LET G=1
  840 FOR x=9 TO 24
  850 IF g(x)=0 THEN LET g=0: LET x=24
  860 NEXT x
  870 IF g=0 THEN RETURN 
  880 PRINT AT 5,11;"WELL DONE!";AT 6,7;"BONUS=1000 X LEVEL"
  890 LET z=1000*l
  900 IF z<10000 THEN PRINT AT 7,13;z
  910 IF z>9999 THEN PRINT AT 7,12;z
  920 LET s=s+z
  930 GO SUB 1490: REM GO SUB HISCORE
  940 LET w=w+1: LET l=l+1
  950 GO SUB 430: GO TO 540: REM GO SUB GET READY GO TO COPTER
  960 RETURN 
  970 REM PAUSE
  980 LET t=t+1
  990 RETURN 
 1000 REM WATER
 1010 PLOT 64,v: DRAW 127,0
 1020 LET v=v+1
 1030 IF v<80 THEN RETURN 
 1040 FOR y=21 TO 12 STEP -1: PRINT AT y,8; PAPER 1;"                ": NEXT y
 1050 FOR y=11 TO 1 STEP -1: PRINT AT y,0; PAPER 1;"                                ": NEXT y:
 1060 FOR f=1 TO 150
 1070 IF INKEY$<>"" THEN FOR x=1 TO 1900: NEXT x: GO TO 1130
 1080 LET x=INT (RND*16): LET y=INT (RND*10): LET d=INT (RND*2)
 1090 PRINT AT 21-y,8+x; INK INT (RND*8); PAPER 1;CHR$ (147+d)
 1100 LET x=INT (RND*32): LET y=INT (RND*11): LET d=INT (RND*2)
 1110 PRINT AT 11-y,0+x; INK INT (RND*8); PAPER 1;CHR$ (147+d)
 1120 NEXT f
 1130 LET y=1
 1140 IF s>h(y) THEN LET e=1
 1150 IF e=1 AND y<10 THEN LET h(y+1)=h(y): LET l(y+1)=l(y): FOR x=1 TO 3: LET n(y,x+1)=n(y,x): NEXT x
 1160 IF e=0 THEN LET y=y+1
 1170 IF e=1 OR y=11 THEN GO TO 1190
 1180 GO TO 1140
 1190 PRINT INK 7; PAPER 1;AT 12,11;"GAME OVER!"
 1200 IF e=1 THEN GO SUB 1530: REM GO SUB INITIALS
 1210 PRINT INK 7; PAPER 1;AT 14,9;"S START";AT 16,9;"I INSTRUCTIONS";AT 18,9;"Q QUIT"
 1220 GO TO 330: REM GO TO KEYS
 1230 REM MAGNET
 1240 LET n=3: LET m=c+d: LET ma=m+1
 1250 LET b=9: IF m>7 AND m<24 THEN LET b=10 
 1260 IF k=1 AND g(ma)=1 THEN LET b=b-1
 1270 FOR y=n TO b
 1280 PRINT INK 0;AT y,m;"\c"; INK 2;AT y+1,m;"\f"
 1290 IF k=1 THEN PRINT INK 3;AT y+2,m;"\a"
 1300 IF q=0 THEN FOR x=1 TO 300: NEXT x: GO TO 1320
 1310 BEEP .025,-y
 1320 NEXT y
 1330 IF g(ma)=1 AND k=0 THEN LET g(ma)=0: LET k=1: GO TO 1380
 1340 IF g(ma)=k THEN GO TO 1380
 1350 IF g(ma)=0 AND k=1 AND (m<9 OR m>22) THEN LET g(ma)=1: LET k=0: GO TO 1370
 1360 IF (g(ma-1)=1 OR g(ma+1)=1) AND m>8 AND m<23 THEN LET g(ma)=1: LET k=0
 1370 IF g(ma)=1 AND m>7 AND m<24 THEN LET s=s+100: GO SUB 1490: REM GO SUB HISCORE
 1380 FOR y=b TO n STEP -1
 1390 PRINT INK 2;AT y,m;"\f"
 1400 IF k=0 THEN PRINT AT y+1,m;" "
 1410 IF k=1 THEN PRINT INK 3;AT y+1,m;"\a"
 1420 IF k=1 AND (g(ma)=0 OR (g(ma)=1 AND n<b)) THEN PRINT AT y+2,m;" "
 1430 IF q=0 THEN FOR x=1 TO 300: NEXT x: GO TO 1450
 1440 BEEP .025,-y
 1450 NEXT y
 1460 GO SUB 1760: REM GO SUB OCTOPUS
 1470 RETURN 
 1480 REM HISCORE
 1490 IF s>h THEN LET h=s
 1500 PRINT AT 0,0; INK 7; PAPER 0;s;AT 0,14;"L";l;AT 0,24;"HI";h
 1510 RETURN 
 1520 REM INITIALS
 1530 PRINT AT 1,0; INK 7; PAPER 0;"HISCORE PRESS ENTER             "
 1540 IF PEEK 23560=13 THEN PAUSE 0
 1550 LET k$=INKEY$
 1560 FOR x=1 TO 1900: NEXT x
 1570 IF k$=CHR$ 13 THEN GO TO 1590
 1580 GO TO 1550
 1590 LET h(y)=s: LET l(y)=l: PRINT AT 1,8; INK 7; PAPER 0;"INITIALS?  ";AT 1,17;
 1600 FOR x=1 TO 3
 1610 LET n(y,x)=CODE CHR$ 32
 1620 NEXT x
 1630 FOR x=1 TO 3
 1640 LET i$=INKEY$
 1650 LET i=CODE i$
 1660 IF i$>="a" AND i$<="z" THEN LET i=i-32
 1670 IF i=13 THEN LET x=3
 1680 IF (i<65 OR i>90) AND i<>13 THEN GO TO 1640
 1690 LET i$=CHR$ i
 1700 LET n(y,x)=CODE i$
 1710 PRINT INK 7; PAPER 0;i$;
 1720 FOR u=1 TO 1900: NEXT u
 1730 NEXT x
 1740 GO TO 160: REM GO TO START
 1750 REM OCTOPUS
 1760 LET m=c+d: LET ma=m+1: LET o=INT (RND*2)
 1770 LET o=1+INT (RND*4-o)
 1780 IF NOT (o=1 AND k=1 AND g(ma)=0 AND (m<8 OR m>23)) THEN RETURN 
 1790 LET i=INT (RND*7)
 1800 FOR y=4 TO 10
 1810 PRINT AT y,m;" "
 1820 PRINT AT y+1,m; INK 3;"\a"
 1830 IF y<10 THEN PRINT AT y+2,m; INK i;"\k"
 1840 IF q=0 THEN FOR x=1 TO 300: NEXT x: GO TO 1870
 1850 BEEP .025,-y+40
 1860 FOR x=1 TO 150: NEXT x
 1870 NEXT y
 1880 LET k=0: LET g(ma)=1
 1890 RETURN