   10 REM HELI Version 1.8 By Phil Hite 2021
   20 FOR g=1 TO 11: READ g$: FOR y=0 TO 7: READ x: POKE USR g$+y,x: NEXT y: NEXT g
   30 DATA "a",255,128,65,34,20,8,255,0
   40 DATA "b",170,85,170,85,170,85,170,85
   50 DATA "c",24,36,24,36,24,36,24,36
   60 DATA "d",2,0,28,186,254,190,28,0
   70 DATA "e",128,0,112,186,254,250,112,0
   80 DATA "f",60,126,255,231,231,231,165,231
   90 DATA "g",7,0,0,128,127,1,0,0
  100 DATA "h",255,32,112,236,226,252,80,252
  110 DATA "i",255,4,14,55,71,63,10,63
  120 DATA "j",224,0,0,1,254,128,0,0
  130 DATA "k",170,170,170,170,254,214,124,56
  140 DIM g(32): LET h=0: LET q=1: DIM h(10): DIM l(10): DIM n(10,9): FOR y=1 TO 10: LET l(y)=1: FOR x=1 TO 3: LET n(y,x)=CODE "A": NEXT x: NEXT y
  150 REM START
  160 LET g=1
  170 BORDER 0: PAPER 7: INK 1: BRIGHT 0: OVER 0: CLS 
  180 PRINT TAB 6;"        HELI    V1.8"
  190 PRINT TAB 6;"   PHIL HITE 2021": PRINT 
  200 PRINT TAB 6;"O LEFT      P RIGHT"
  210 PRINT TAB 6;"SPACE PICK UP/DROP"
  220 PRINT TAB 6;"M SOUND O"
  230 IF q=1 THEN PRINT AT 5,15;"FF";
  240 IF q=0 THEN PRINT AT 5,15;"N ";
  250 PRINT " H HOLD"
  260 PRINT TAB 6;"  BUILD THE BRIDGE"
  270 PRINT TAB 6;"   STOP THE FLOOD"
  280 PRINT TAB 6;"BEWARE OF OCTOPUSES!"
  290 PRINT TAB 6;"S START     Q QUIT": PRINT 
  300 PRINT TAB 6;"  TODAY";CHR$ 39;"S GREATEST"
  310 FOR y=1 TO 10: PRINT AT y+11,9-LEN (STR$ (y));y;AT y+11,10;h(y);AT y+11,17;"L";l(y);AT y+11,21;: FOR x=1 TO 3: LET i=n(y,x): PRINT CHR$  i;: NEXT x: NEXT y
  320 REM KEYS
  330 LET k$=INKEY$
  340 IF k$="m" OR k$="i" OR k$="s" OR k$="q" THEN LET k$=CHR$ (CODE k$-32)
  350 IF k$="M" THEN FOR x=1 TO 1900: NEXT x: LET q=NOT q
  360 IF q=0 AND g=1 THEN PRINT AT 5,15;"N "
  370 IF q=1 AND g=1 THEN PRINT AT 5,15;"FF"
  380 IF k$="I" AND g=0 THEN GO TO 160: REM GO TO START
  390 IF k$="S" THEN CLS : RANDOMIZE : LET s=0: LET l=1: LET w=0: LET k=0: LET u=0: LET e=0: LET t=0: GO SUB 430: GO TO 530: REM GO SUB GET READY GO TO COPTER
  400 IF k$="Q" THEN STOP 
  410 GO TO 330
  420 REM GET READY
  430 PRINT AT 8,9;"GET READY FOR";AT 9,11;"LEVEL ";l;"!"
  440 IF q=0 THEN FOR x=1 TO 15000: NEXT x: GO TO 490
  450 BEEP .125,0: BEEP .125,4: BEEP .125,7
  460 BEEP .125,2: BEEP .125,5: BEEP .125,9
  470 BEEP .125,7: BEEP .125,9: BEEP .125,11
  480 BEEP .375,12
  490 CLS 
  500 LET c=15: LET d=0
  510 RETURN 
  520 REM COPTER
  530 LET i=INT (RND*8): LET p=INT (RND*8): IF i=7 AND p=7 THEN GO TO 530
  540 FOR y=12 TO 21: PRINT AT y,0; INK i; PAPER p;"\b\b\b\b\b\b\b\b";AT y,24;"\b\b\b\b\b\b\b\b": NEXT y
  550 PRINT AT 0,0; PAPER 0;"                                ": PRINT AT 0,0; INK 7; PAPER 0;s;AT 0,14;"L";l;AT 0,24;"HI";h
  560 PRINT AT 11,0; INK 3;"\a\a\a\a\a\a\a\a";AT 11,24;"\a\a\a\a\a\a\a\a"
  570 FOR x=1 TO 32
  580 LET g(x)=0: IF x<9 OR x>24 THEN LET g(x)=1
  590 NEXT x
  600 LET v=w
  610 IF d=0 THEN PRINT AT 2,c; INK 4;"\i\j"
  620 IF d=1 THEN PRINT AT 2,c; INK 4;"\g\h"
  630 PRINT AT 3,c+d; INK 2;"\f"
  640 IF k=1 THEN PRINT AT 4,c+d; INK 3;"\a"
  650 REM DELAY FOR COMPILER
  660 FOR x=1 TO 450: NEXT x
  670 GO SUB 820: GO SUB 970: IF t=6 THEN GO SUB 1000: LET t=0: REM GO SUB BRIDGE GO SUB PAUSE GO SUB WATER
  680 LET k$=INKEY$
  690 IF k$="m" OR k$="o" OR k$="p" OR k$="d" OR k$="h" OR k$="q" THEN LET k$=CHR$ (CODE k$-32)
  700 IF k$=" " THEN GO SUB 1230: REM GO SUB MAGNET
  710 IF k$="O" AND d=0 AND c>0 THEN PRINT AT 2,c+1;" ";AT 3,c;" ";AT 4,c;" ": LET c=c-1
  720 IF k$="O" AND d=1 THEN LET d=0: PRINT AT 3,c+1;" ";AT 4,c+1;" "
  730 IF k$="P" AND d=1 AND c<30 THEN PRINT AT 2,c;" ";AT 3,c+1;" ";AT 4,c+1;" ": LET c=c+1
  740 IF k$="P" AND d=0 THEN LET d=1: PRINT AT 3,c;" ";AT 4,c;" "
  750 IF k$="H" AND u=0 THEN PRINT AT 6,12;"HOLD IT!": PRINT AT 8,9;"ENTER CONTINUE": LET u=1
  760 IF INKEY$=CHR$ 13 AND u=1 THEN PRINT AT 6,12;"        ";AT 8,9;"              ": LET u=0
  770 IF u=1 THEN GO TO 760
  780 IF k$="M" THEN LET q=NOT q: FOR x=1 TO 1900: NEXT x
  790 IF k$="Q" THEN GO TO 160: REM GO TO START
  800 GO TO 610
  810 REM BRIDGE
  820 LET G=1
  830 FOR x=9 TO 24
  840 IF g(x)=0 THEN LET g=0: LET x=24
  850 NEXT x
  860 IF g=0 THEN RETURN 
  870 PRINT AT 5,11;"WELL DONE!";AT 6,7;"BONUS=1000 X LEVEL"
  880 LET z=1000*l
  890 IF z<10000 THEN PRINT AT 7,13;z
  900 IF z>9999 THEN PRINT AT 7,12;z
  910 LET s=s+z
  920 GO SUB 1480: REM GO SUB HISCORE
  930 LET w=w+1: LET l=l+1
  940 GO SUB 420: GO TO 530: REM GO SUB GET READY GO TO COPTER
  950 RETURN 
  960 REM PAUSE
  970 LET t=t+1
  980 RETURN 
  990 REM WATER
 1000 PLOT 64,v: DRAW 127,0
 1010 LET v=v+1
 1020 IF v<80 THEN RETURN 
 1030 FOR y=21 TO 12 STEP -1: PRINT AT y,8; PAPER 1;"                ": NEXT y
 1040 FOR y=11 TO 1 STEP -1: PRINT AT y,0; PAPER 1;"                                ": NEXT y:
 1050 FOR f=1 TO 150
 1060 IF INKEY$<>"" THEN FOR x=1 TO 1900: NEXT x: GO TO 1120
 1070 LET x=INT (RND*16): LET y=INT (RND*10): LET d=INT (RND*2)
 1080 PRINT AT 21-y,8+x; INK INT (RND*8); PAPER 1;CHR$ (147+d)
 1090 LET x=INT (RND*32): LET y=INT (RND*11): LET d=INT (RND*2)
 1100 PRINT AT 11-y,0+x; INK INT (RND*8); PAPER 1;CHR$ (147+d)
 1110 NEXT f
 1120 LET y=1
 1130 IF s>h(y) THEN LET e=1
 1140 IF e=1 AND y<10 THEN LET h(y+1)=h(y): LET l(y+1)=l(y): FOR x=1 TO 3: LET n(y,x+1)=n(y,x): NEXT x
 1150 IF e=0 THEN LET y=y+1
 1160 IF e=1 OR y=11 THEN GO TO 1180
 1170 GO TO 1130
 1180 PRINT INK 7; PAPER 1;AT 12,11;"GAME OVER!"
 1190 IF e=1 THEN GO SUB 1520: REM GO SUB INITIALS
 1200 PRINT INK 7; PAPER 1;AT 14,9;"S START";AT 16,9;"I INSTRUCTIONS";AT 18,9;"Q QUIT"
 1210 GO TO 330: REM GO TO KEYS
 1220 REM MAGNET
 1230 LET n=3: LET m=c+d: LET ma=m+1
 1240 LET b=9: IF m>7 AND m<24 THEN LET b=10 
 1250 IF k=1 AND g(ma)=1 THEN LET b=b-1
 1260 FOR y=n TO b
 1270 PRINT INK 0;AT y,m;"\c"; INK 2;AT y+1,m;"\f"
 1280 IF k=1 THEN PRINT INK 3;AT y+2,m;"\a"
 1290 IF q=0 THEN FOR x=1 TO 300: NEXT x: GO TO 1310
 1300 BEEP .025,-y
 1310 NEXT y
 1320 IF g(ma)=1 AND k=0 THEN LET g(ma)=0: LET k=1: GO TO 1370
 1330 IF g(ma)=k THEN GO TO 1370
 1340 IF g(ma)=0 AND k=1 AND (m<9 OR m>22) THEN LET g(ma)=1: LET k=0: GO TO 1360
 1350 IF (g(ma-1)=1 OR g(ma+1)=1) AND m>8 AND m<23 THEN LET g(ma)=1: LET k=0
 1360 IF g(ma)=1 AND m>7 AND m<24 THEN LET s=s+100: GO SUB 1480: REM GO SUB HISCORE
 1370 FOR y=b TO n STEP -1
 1380 PRINT INK 2;AT y,m;"\f"
 1390 IF k=0 THEN PRINT AT y+1,m;" "
 1400 IF k=1 THEN PRINT INK 3;AT y+1,m;"\a"
 1410 IF k=1 AND (g(ma)=0 OR (g(ma)=1 AND n<b)) THEN PRINT AT y+2,m;" "
 1420 IF q=0 THEN FOR x=1 TO 300: NEXT x: GO TO 1440
 1430 BEEP .025,-y
 1440 NEXT y
 1450 GO SUB 1750: REM GO SUB OCTOPUS
 1460 RETURN 
 1470 REM HISCORE
 1480 IF s>h THEN LET h=s
 1490 PRINT AT 0,0; INK 7; PAPER 0;s;AT 0,14;"L";l;AT 0,24;"HI";h
 1500 RETURN 
 1510 REM INITIALS
 1520 PRINT AT 1,0; INK 7; PAPER 0;"HISCORE PRESS ENTER             "
 1530 IF PEEK 23560=13 THEN PAUSE 0
 1540 LET k$=INKEY$
 1550 FOR x=1 TO 1900: NEXT x
 1560 IF k$=CHR$ 13 THEN GO TO 1580
 1570 GO TO 1540
 1580 LET h(y)=s: LET l(y)=l: PRINT AT 1,8; INK 7; PAPER 0;"INITIALS?  ";AT 1,17;
 1590 FOR x=1 TO 3
 1600 LET n(y,x)=CODE CHR$ 32
 1610 NEXT x
 1620 FOR x=1 TO 3
 1630 LET i$=INKEY$
 1640 LET i=CODE i$
 1650 IF i$>="a" AND i$<="z" THEN LET i=i-32
 1660 IF i=13 THEN LET x=3
 1670 IF (i<65 OR i>90) AND i<>13 THEN GO TO 1630
 1680 LET i$=CHR$ i
 1690 LET n(y,x)=CODE i$
 1700 PRINT INK 7; PAPER 0;i$;
 1710 FOR u=1 TO 1900: NEXT u
 1720 NEXT x
 1730 GO TO 160: REM GO TO START
 1740 REM OCTOPUS
 1750 LET m=c+d: LET ma=m+1: LET o=INT (RND*2)
 1760 LET o=1+INT (RND*4-o)
 1770 IF NOT (o=1 AND k=1 AND g(ma)=0 AND (m<8 OR m>23)) THEN RETURN 
 1780 LET i=INT (RND*7)
 1790 FOR y=4 TO 10
 1800 PRINT AT y,m;" "
 1810 PRINT AT y+1,m; INK 3;"\a"
 1820 IF y<10 THEN PRINT AT y+2,m; INK i;"\k"
 1830 IF q=0 THEN FOR x=1 TO 300: NEXT x: GO TO 1860
 1840 BEEP .025,-y+40
 1850 FOR x=1 TO 150: NEXT x
 1860 NEXT y
 1870 LET k=0: LET g(ma)=1
 1880 RETURN